apply plugin: 'maven-publish'
apply plugin: 'signing'

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign } && project.ext.isReleaseVersion) {
        def id = System.getenv('GPG_ID')
        def file = '/home/travis/.gnupg/secring.gpg'
        def password = System.getenv('GPG_PASSWORD')

        allprojects { ext."signing.keyId" = id }
        allprojects { ext."signing.secretKeyRingFile" = file }
        allprojects { ext."signing.password" = password }
    }
}

ext {
    isReleaseVersion = !(projectVersion =~ /-SNAPSHOT$/)
    isNeedSign = System.getenv('GPG_ID') && isReleaseVersion
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'gRPC Spring Boot Starter'
                description = 'gRPC Spring Boot Starter'
                url = 'https://github.com/yidongnan/grpc-spring-boot-starter'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'adam'
                        name = 'adam gao'
                        email = 'gaoyu-jx@founder.com.cn'
                    }
                    developer {
                        id = 'huangguochao'
                        name = 'huangguochao'
                        email = 'huangguochao@founder.com.cn'
                        organization = 'inno-center'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/pku-hit/grpc-spring-boot-starter.git'
                    developerConnection = 'scm:git:ssh@github.com:pku-hit/grpc-spring-boot-starter.git'
                    url = 'https://github.com/pku-hit/grpc-spring-boot-starter'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv('INNO_MVN_USER')
                password System.getenv('INNO_MVN_PWD')
            }
            if (project.ext.isReleaseVersion) {
                url "https://repo.rdc.aliyun.com/repository/9320-release-B4xCku/"
            } else {
                url "https://repo.rdc.aliyun.com/repository/9320-snapshot-iV78kb/"
            }
        }
    }

    signing {
        required { project.ext.isNeedSign }
        sign publishing.publications.mavenJava
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}
